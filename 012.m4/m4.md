#m4について
##
参考
+ https://www.ibm.com/support/knowledgecenter/ja/ssw_aix_71/com.ibm.aix.cmds3/m4.htm
## m4とは
m4 は、ブライアン・カーニハンとデニス・リッチーが設計した汎用テキストマクロプロセッサである。その名称は「macro」（マクロ）の「m」と、AP-3ミニコンピュータでデニス・リッチーがそれ以前に書いたマクロプロセッサ「m3」の次、というところから来ている。(wikipedia)
## m4を使っているツール
### autoconf
autoconfはconfigureスクリプトを作るためのツールである。autoconfが生成
するconfigure.inには様々なm4マクロが埋め込まれている。

## 用途
### HelloWorld

例えば以下のように定義して・・・
```
#Helloという文字列をHとして定義する
define(`H',`Hello')
#Worldという文字列をWとして定義する
define(`W',`World')
#Helloに置き換わる
H
#Worldに置き換わる
W
```
m4コマンドで展開するとこうなる
```
$ m4 hello.m4 
#Helloという文字列をHとして定義する

#Worldという文字列をWとして定義する

#Helloに置き換わる
Hello
#Worldに置き換わる
World

```
### 文字列置換
文字列置換にも使える
```
$ cat convert.m4 
define(`NAME', `Billy')
NAME is a king of locker.
$ m4 convert.m4 

Billy is a king of locker.
```

### マクロの引数
```
$ cat macroparam.m4 
define(`pair',`
	$1 ---> $2
')

pair(left, right)
pair(up, down)
pair(front, back)

```
以下のように解釈される
```
$ m4 macroparam.m4 



	left ---> right


	up ---> down


	front ---> back
```

### undefineで定義されたマクロを無効化する
こう書くと・・・
```
$ cat undefine.m4 
define(`pair',`
	$1 ---> $2
')
undefine(`pair')
pair(left, right)
pair(up, down)
pair(front, back)

```
pair()は無効化されていて展開されない
```
$ m4 undefine.m4 


pair(left, right)
pair(up, down)
pair(front, back)
```

### includeでマクロを再利用
こうかくと・・・
```
miyagawa@miyagawa-dynabook-Satellite-B551-E:~/work/github/memo/012.m4$ cat include.m4 
include(`macroparam.m4')
pair(A,B)
```
こうなる
```
m4 include.m4 



	left ---> right


	up ---> down


	front ---> back



	A ---> B

```
存在しないファイルをincludeしてエラーを無視したければsincludeを使う

### ifdef, ifelseディレクティブ
cppの#ifdefと同じように捉えれば良い
```
ifdef(NAME,ifのケース, elseのケース)
```
例えば以下になる
文字列testがdefineされていたらif caseとして展開され、されていなければelse caseと
して展開される。下記では-Dtestでdefineしているがdefine(`test')をファイルに書いても
同じ。
```
$ cat if.m4 
ifdef(`test', `if case', `else case')

$ m4 if.m4 else case

$ m4 -Dtest if.m4 
if case

```
ifdef()より先にdefine()が解釈されていること
```
$ cat if.m4 
define(`test')
ifdef(`test', `if case', `else case')
```
cppの#ifに対応する書き方は以下、testが1として定義されている時、それ以外の時・・・
```
$ cat ifelse.m4 
ifelse(test,1,`if case',`else case')


$ m4 -Dtest=1 ifelse.m4 
if case

$ m4 -Dtest=2 ifelse.m4 
else case

```
else条件を書くこともできる
```
$ cat ifelse.m4 
ifelse(test,1,`test is 1', test, 2, `test is 2', `else case')

$ m4 -Dtest=1 ifelse.m4 
1 is 1

$ m4 -Dtest=2 ifelse.m4 
2 is 2

$ m4 -Dtest=3 ifelse.m4 
else case

```
###再起によるループ
$#は引数の数。$@はすべての引数。
shift()は先頭引数を除去するディレクティブ
最後の引数になるまで先頭から引数が除去されつつloopとして展開される。

```
$ cat loop.m4 
#一番最後のパラメタだけ標準出力
define(`loop', `
	ifelse($#, 1, 
		$1, 
		`loop(shift($@))
	')
')
loop(a,b,c,d,e,f,g,h,i,j,k)
miyagawa@miyagawa-dynabook-Satellite-B551-E:~/work/github/memo/012.m4$ m4 loop.m4 
#一番最後のパラメタだけ標準出力

	k

```
###forループ用マクロを定義してみる
for(`イテレータ', イテレータ初期値, イテレータ終了値, `処理')
とする。

```
$ cat for.m4 
define(`for',`pushdef(`$1', `$2')_for(`$1', `$2', `$3', `$4')popdef(`$1')')
define(`_for',`$4`'ifelse($1, `$3', ,`define(`$1', incr($1))_for(`$1', `$2', `$3', `$4')')')
$ cat usefor.m4 
include(for.m4)
for(`i', 1, 10, ``i'=i')
$ m4 usefor.m4 



i=1i=2i=3i=4i=5i=6i=7i=8i=9i=10

```
### SELinuxのallowとneverallowを同時に定義する
例えば
```
allow domain_t type1_t:file read;
allow domain_t type2_t:file write;
allow domain_t type3_t:file create;
```
という定義が必要であったとする。
これ以外のObjectClass=fileに対するアクセスをすべてneverallowにするには以下のように書くべきである。
```
#type1_tへのread以外のアクセスを禁止
neverallow domain_t type1_t:file ~{read}
#type2_tへのwrite以外のアクセスを禁止
neverallow domain_t type2_t:file ~{write}
#type3_tへのcreate以外のアクセスを禁止
neverallow domain_t type3_t:file ~{create}
#type1_t,type2_t,type3_t以外のfileへのすべてのアクセスを禁止
neverallow domain_t ~{type1_t type2_t type3_t}:file *
```
これをマクロで簡単に定義するために以下のようなマクロを用意した。
```
define(`_both_directive',`
	allow domain class:$1 $2;
	neverallow domain class:$1 ~{$2};
	
	ifelse($#, 2, `
		define(`object_type', object_type `$1')
	',`
		ifdef(`object_type', `
			define(`object_type', object_type `$1')
			_both_directive(shift(shift($@)))
		',`
			define(`object_type', `$1')
			_both_directive(shift(shift($@)))
		')
	')
')

define(`both_directive', `
	define(`domain', `$1')
	define(`class', `$2')
	_both_directive(shift(shift($@)))
	neverallow domain class:~{object_type} *;
	undefine(`object_type') 
')
```
このマクロは以下のように使用す
```
#マクロが定義されたファイルをインクルード
include(allow_generator.m4)

#both_directive(<ドメイン>, <ObjectClass>, <タイプ>, <アクセスベクタ>, <タイプ2>, <アクセスベクタ2>, ...)
both_directive(domain_t, file, test1_t, write, test2_t, read, type3_t, create)
both_directive(domain_t, file, testA_t, write, testB_t, read, typeB_t, create)
```
このマクロが展開されると以下のようになる。
```
$ m4 test_policy.if
	allow domain_t file:test1_t write;
	neverallow domain_t file:test1_t ~{write};
	allow domain_t file:test2_t read;
	neverallow domain_t file:test2_t ~{read};
	allow domain_t file:type3_t create;
	neverallow domain_t file:type3_t ~{create};
	neverallow domain_t file:~{test1_t test2_t type3_t} *;
	allow domain_t file:testA_t write;
	neverallow domain_t file:testA_t ~{write};
	allow domain_t file:testB_t read;
	neverallow domain_t file:testB_t ~{read};
	allow domain_t file:typeB_t create;
	neverallow domain_t file:typeB_t ~{create};
	neverallow domain_t file:~{testA_t testB_t typeB_t} *;
```